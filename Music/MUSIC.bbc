   10  GOSUB 1750:GOSUB1760:GOSUB 1130
   20  PRINT:PRINT"->";:GOSUB 1720:PRINT I$:LET C=0
   30  FOR I=1 TO 12:IF I$=MID$("ZLIDNPRCMKST",I,1) THEN LET C=I
   40  NEXT I:IF C=0 THEN GOTO 20
   50  ON C GOSUB 100,560,420,490,1030,1220,860,1100,330,1480,1670,70
   60  GOTO 20
   70  PRINT "SET TEMPO: (1 - 15)"
   80  INPUT TP:IF TP<1 OR TP>15 THEN GOTO 80
   90  LET TP=(0.5+TP/10)/7.5:RETURN
  100  GOSUB 1750:PRINT "1.ADD REPEAT":PRINT"2.REMOVE REPEAT"
  110  PRINT "3.REPEAT FROM BEGINNING":PRINT
  120  INPUT C:IF C<1 OR C>3 THEN GOTO 120
  130  ON C GOSUB 150,220,270
  140  RETURN
  150  IF BX=7 THEN RETURN
  160  PRINT "FROM BAR NUMBER";:INPUT B:IF B<1 THEN LET B=1
  170  LET N=0:FOR I=1 TO 5 STEP 2:IF B(I,1)=B THEN LET N=1
  180  NEXT I:IF N<>0 THEN RETURN
  190  LET B(BX,1)=B:LET BX=BX+1
  200  PRINT "TO BAR NUMBER";:INPUT B:IF B<=B(BX-1,1) THEN GOTO 200
  210  LET B(BX,1)=B:LET BX=BX+1:GOSUB 290:RETURN
  220  PRINT"STARTING AT BAR NUMBER";:INPUT B
  230  LET N=0:FOR I=1 TO BX-1:IF B(I,1)=B THEN LET N=1
  240  NEXT I:IF N=0 THEN RETURN
  250  LET B(N,1)=1000:LET B(N+1,1)=1000
  260  GOSUB 290 : RETURN
  270  PRINT "AT BAR NUMBER";:INPUT RB:IF RB<0 THEN LET RB=0
  280  RETURN
  290  FOR I=1 TO 6:LET L=2000
  300  FOR J=I TO 7:IF B(J,1)=<L THEN LET L=B(J,1):LET N=J
  310  NEXT J:LET T=B(I,1):LET B(I,1)=B(N,1):LET B(N,1)=T
  320  NEXT I : RETURN
  330  GOSUB 1750:PRINT:PRINT "PITCHES:"
  340  LET C=1:FOR I=1 TO 29
  350  PRINT N$(I);" ";:IF C=5 THEN PRINT:LET C=0
  360  IF I=12 OR I=26 THEN PRINT:LET C=0 
  370  LET C=C+1:NEXT I
  380  PRINT:PRINT:PRINT "DURATIONS:"
  390  PRINT "4*   4   2*   2"
  400  PRINT "1*   1":PRINT "1/2* 1/2 1/4* 1/4"
  410  RETURN
  420  IF TM=MN THEN GOSUB 1710:RETURN
  430  PRINT"NOTE NUMBER";:INPUT NP
  440  IF NP=0 OR NP>TN THEN RETURN
  450  FOR I=TN+1 TO NP STEP-1
  460  LET M(I+1,1)=M(I,1):LET M(I+1,2)=M(I,2)
  470  NEXT I:GOSUB 930
  480  LET TN=TN+1:LET B=NP:GOSUB 570:RETURN
  490  IF TN=0 THEN RETURN
  500  PRINT "NOTO NUMBER";:INPUT NP
  510  IF NP>TN OR NP=0 THEN RETURN
  520  FOR I=NP TO TN
  530  LET M(I,1)=M(I+1,1):LET M(I,2)=M(I+1,2)
  540  NEXT I
  550  LET TN=TN-1:LET B=NP:GOSUB 570:RETURN
  560  LET B=1:GOSUB 570:RETURN
  570  GOSUB 1750:LET LB=0:IF B<1 THEN LET B=1
  580  IF TN=0 THEN PRINT "NO NOTES":RETURN
  590  LET NB=1:LET BP=1:LET RP=1
  600  FOR I=1 TO B:LET LB=LB+D(M(I,2))
  610  IF LB>=TB AND I<B THEN LET LB=0:LET NB=NB+1:LET BP=I+1
  620  IF LB=0 AND B(RP,1)=NB THEN LET RP=RP+1:GOTO 620
  630  NEXT I:LET B=BP:LET LB=0:GOSUB 760
  640  LET I=0
  650  IF M(I+1,1)<=0 THEN RETURN
  660  PRINT TAB(1);I+B;TAB(5);N$(M(I+B,1));TAB(10);D$(M(I+B,2))
  670  LET LB=LB+D(M(I+B,2)):IF LB>TB THEN PRINT "BAR ERROR"
  680  IF LB>=TB THEN GOSUB 790
  690  IF LB=0 AND I>6 AND I+B<TN THEN GOSUB 740
  700  IF I+B<TN AND I$=CHR$(13) THEN LET I=I+1:GOTO 650
  710  LET NB=NB+1
  720  IF LB<>0 THEN PRINT "BAR ERROR":GOSUB 850
  730  RETURN
  740  GOSUB 1720:IF I$=CHR$(13) THEN RETURN
  750  GOSUB 1750:GOSUB 760:LET B=B+I:LET I=0:RETURN
  760  PRINT "TIME ";5-R;"/4":GOSUB 850
  770  IF B(RP,1)=NB THEN PRINT "RPB":LET RP=RP+1
  780  RETURN
  790  NB=NB+1:LET LB=0
  800  IF B(RP,1)-NB AND B(RP,2)=0 THEN PRINT "RPE":LET RP=RP+1
  810  IF NB=RB THEN PRINT "RFB"
  820  GOSUB 850:IF I>6 THEN RETURN
  830  IF B(RP,1)=NB AND B(RP,2)=1 THEN PRINT "RPB":LET RP=RP+1
  840  RETURN
  850  PRINT "--------------";NB:RETURN
  860  GOSUB 1750:PRINT"SELECT RHYTHM:"
  870  PRINT:PRINT"0 = OFF"
  880  IF R=1 THEN PRINT "1 = RHYTHM 1":PRINT "2 = RHYTHM 2"
  890  IF R=2 THEN PRINT "1 = ON"
  900  INPUT C:LET C=INT(ABS(C)):IF (C>1 AND R=2) OR (C>2 AND R=1) THEN GOTO 900
  910  LET RS=0:IF R=1 AND C=2 THEN LET RS=D(6)
  920  LET RF=SGN(C):GOSUB 1750:RETURN
  930  LET L$="":PRINT TAB(1);NP;":";:INPUT L$
  940  IF L$="" THEN RETURN
  950  LET K$=LEFT$(L$,3):LET N=0
  960  FOR I=1 TO 29:IF N$(I)=K$ THEN LET N=I
  970  NEXT I:IF N=0 THEN PRINT "PITCH ERROR":GOTO 930
  980  LET M(NP,1)=N
  990  LET K$=RIGHT$(L$,LEN(L$)-4):LET N=0
 1000  FOR I=1 TO 10:IF D$(I)=K$ THEN LET N=I
 1010  NEXT I:IF N=0 THEN PRINT "DURATION ERROR":GOTO 930
 1020  LET M(NP,2)=N:RETURN
 1030  PRINT "START FROM";:INPUT NP:LET PM=NP
 1040  IF NP>TN+1 OR NP=0 OR NP>MN THEN RETURN
 1050  GOSUB 930
 1060  IF L$="" THEN LET B=PM:GOSUB 570:RETURN
 1070  IF NP=TN THEN LET TN=NP:LET M(TN+1,1)=0
 1080  LET NP=NP+1:IF NP<=MN THEN GOTO 1050
 1090  GOSUB 1710:RETURN
 1100  PRINT "CLEAR - SURE (Y/N)?"
 1110  GOSUB 1720:IF I$="Y" THEN GOSUB 1130
 1120  RETURN
 1130  FOR I=1 TO MN:LET M(I,1)=0:NEXT I
 1140  LET TP=1/7.5:LET RF=0:LET TN=0:LET BX=1:LET RB=0
 1150  FOR I=1 TO 7:LET B(I,2)=I-2*INT(I/2):NEXT I
 1170  LET C=1:GOSUB 1690
 1180  PRINT "SELECT TIME:"
 1190  PRINT "1 = 4/4":PRINT "2 = 3/4"
 1200  INPUT R:IF R<>1 AND R<>2 THEN GOTO 1200
 1210  LET TB=D(1+R):LET DR=D(5-R):LET RS=0:GOSUB 1750:RETURN
 1220  LET NR=RS:LET NM=0:LET BB=RB:GOSUB 1450
 1230  TIME=0
 1240  LET T=TIME*TP
 1250  IF NM<T THEN GOSUB 1310
 1260  IF NR<T AND RF=1 THEN GOSUB 1290
 1270  IF INKEY$(0)="" THEN GOTO 1240
 1280  RETURN
 1290  SOUND &0010,2,5,10
 1300  LET NR=NR+DR:RETURN
 1310  IF M(PM,1)=0 THEN RETURN
 1320  IF M(PM,1)=26 THEN GOTO 1340
 1330  SOUND &0011,1,P(M(PM,1)),D(M(PM,2))/TP/5
 1340  LET NM=NM+D(M(PM,2))
 1350  LET LB=LB+D(M(PM,2))
 1360  LET PM=PM+1
 1370  IF LB>=TB THEN GOSUB 1400
 1380  IF NB=BB THEN GOSUB 1450:LET BB=0
 1390  RETURN
 1400  LET LB=0:LET NB=NB+1
 1410  IF B(RP,1)<>NB THEN RETURN
 1420  IF B(RP,2)=0 THEN LET PM=PP:LET NB=ZB
 1430  IF B(RP,2)=1 THEN LET PP=PM:LET ZB=NB
 1440  LET RP=RP+1:RETURN
 1450  LET PM=1:LET NB=1:LET PP=1:LET LB=0:LET ZB=1:LET RP=1
 1460  IF B(1,1)=1 THEN LET RP=2
 1470  RETURN
 1480  LET OC=0:GOSUB 1750:PRINT:PRINT "KEYBOARD:"
 1490  PRINT "RECORD?":PRINT "1 = ON":PRINT "0 = OFF"
 1500  INPUT C:LET C=ABS(SGN(C))
 1510  IF C=0 THEN LET PM=0:GOTO 1540
 1520  PRINT "START FROM ";:INPUT PM:IF PM<1 OR PM>TN+1 OR PM=MN THEN GOTO 1520
 1530  LET PM=PM-1
 1540  LET N=0:GOSUB 1720
 1550  FOR I=1 TO 14:IF I$=X$(I) THEN LET N=I:LET I=14
 1560  NEXT I:IF N=0 THEN GOTO 1620
 1570  IF N=14 THEN LET N=26:GOTO 1600
 1580  LET N=N+12*OC
 1590  SOUND &0011,1,P(N),10
 1600  PRINT N$(N);" ";
 1610  IF C=1 THEN LET PM=PM+1:LET M(PM,1)=N:LET M(PM,2)=6
 1620  IF I$="P" THEN LET OC=1-OC
 1630  IF I$<>CHR$(13) AND PM<MN THEN GOTO 1540
 1640  IF PM>TN THEN LET TN=PM
 1650  IF PM=MN THEN PRINT:GOSUB 1710
 1660  PRINT:RETURN
 1670  GOSUB 1750:PRINT:PRINT "SELECT PRESET (1-5)"
 1680  INPUT C:IF C<1 OR C>5 THEN GOTO 1670
 1690  ON C GOSUB 1860,1870,1880,1890,1900
 1700  RETURN
 1710  PRINT "OUT OF SPACE":RETURN
 1720  I$=INKEY$(0)
 1730  IF I$="" THEN GOTO 1720
 1740  RETURN
 1750  CLS:RETURN
 1760  LET MN=100
 1770  DIM N$(29),P(25),D$(10),D(10)
 1780  DIM M(MN+1,2),X$(14),B(7,2)
 1790  FOR I=1 TO 29:READ N$(I):NEXT I
 1800  FOR I=1 TO 10:READ D$(I):NEXT I
 1810  GOSUB 2000
 1820  FOR I=1 TO 13:READ X$(I):NEXT I:LET X$(4)=" "
 1830  FOR I= 1 TO 25:P(I)=1+4*I:NEXT I
 1840  ENVELOPE 2,1,0,0,0,0,0,0,126,-8,0,-1,126,0
 1850  RETURN
 1860  ENVELOPE 1,1,0,0,0,0,0,0,126,-8,0,-1,126,0:RETURN
 1870  ENVELOPE 1,1,0,0,0,0,0,0,126,-2,0,-1,126,0:RETURN
 1880  ENVELOPE 1,1,0,0,0,0,0,0,32,0,-4,-1,126,126:RETURN
 1890  ENVELOPE 1,2,-1,1,-1,1,2,1,32,0,-4,-1,126,126:RETURN
 1900  ENVELOPE 1,1,0,0,0,0,0,0,63,-4,-2,-1,126,100:RETURN 
 1910  DATA C-1,C#1,D-1,D#1,E-1,F-1,F#1,G-1,G#1,A-1,A#1
 1920  DATA B-1,B#1,C-2,C#2,D-2,D#21,E-21,F-2,F#2,G-2,G#2,A-2,A#2 
 1930  DATA A#2,B-2,B#2,C-3,"RST"
 1940  DATA "4*","4","2*","2","1*","1","1/2*","1/2","1/4*","1/4"
 1950  DATA 48,32,24,16,12,8,6,4,3,2
 1960  DATA "Q","2","W","3","E","R",5","T","6","Y","7","U","I"
 2000  D(1)=48:D(2)=32:D(3)=24:D(4)=16:D(5)=12:D(6)=8:D(7)=6:D(8)=4:D(9)=3:D(10)=2:RETURN
